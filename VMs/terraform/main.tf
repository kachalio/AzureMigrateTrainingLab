# main.tf
# Purpose: Main template file that deploys the training lab

# vSphere
provider "vsphere" {
  user                 = var.vsphere_user
  password             = var.vsphere_password
  vsphere_server       = var.vsphere_server
  allow_unverified_ssl = true
}

data "vsphere_datacenter" "dc" {
  name = var.vsphere_datacenter
}

data "vsphere_compute_cluster" "cluster" {
  name          = var.vsphere_cluster
  datacenter_id = data.vsphere_datacenter.dc.id
}

# VM
data "vsphere_datastore" "ds" {
  name          = var.vm_datastore
  datacenter_id = data.vsphere_datacenter.dc.id
}

data "vsphere_network" "network" {
  name          = var.vm_network
  datacenter_id = data.vsphere_datacenter.dc.id
}

data "vsphere_virtual_machine" "linuxtemplate" {
  name          = "/${var.vsphere_datacenter}/vm/${var.vsphere_template_folder}/${var.linuxvm_template_name}"
  datacenter_id = data.vsphere_datacenter.dc.id
}

data "vsphere_virtual_machine" "wintemplate" {
  name          = "/${var.vsphere_datacenter}/vm/${var.vsphere_template_folder}/${var.winvm_template_name}"
  datacenter_id = data.vsphere_datacenter.dc.id
}

# Random string for unique VM names

resource "random_string" "vm_suffix" {
  length  = 6
  upper   = true
  lower   = false
  numeric = true
  special = false
}


# Create VMs

#linux
resource "vsphere_virtual_machine" "linuxvm" {
  count = var.linuxvm_count

  name             = "${var.linuxvm_name_prefix}-${count.index + 1}"
  resource_pool_id = data.vsphere_compute_cluster.cluster.resource_pool_id
  datastore_id     = data.vsphere_datastore.ds.id
  folder           = var.vm_folder
  num_cpus         = var.linuxvm_cpu
  memory           = var.linuxvm_ram
  guest_id         = data.vsphere_virtual_machine.linuxtemplate.guest_id
  firmware         = data.vsphere_virtual_machine.linuxtemplate.firmware
  scsi_type        = data.vsphere_virtual_machine.linuxtemplate.scsi_type

  network_interface {
    network_id   = data.vsphere_network.network.id
    adapter_type = data.vsphere_virtual_machine.linuxtemplate.network_interface_types[0]
  }

  disk {
    label            = "${var.linuxvm_name_prefix}-${count.index + 1}-disk"
    size             = data.vsphere_virtual_machine.linuxtemplate.disks[0].size
    thin_provisioned = data.vsphere_virtual_machine.linuxtemplate.disks[0].thin_provisioned

    unit_number = data.vsphere_virtual_machine.linuxtemplate.disks[0].unit_number
  }

  clone {
    template_uuid = data.vsphere_virtual_machine.linuxtemplate.id
    # customize {
    #   linux_options {
    #   #   host_name = "${each.value.alias}-${var.linuxvm_name}" //-${count.index + 1}"
    #   #   domain    = var.linuxvm_domain
    #   #   time_zone = "America/New_York"
    #   }
    #   network_interface {}
    #   timeout = 30
    # }
  }
}

resource "vsphere_virtual_machine" "winvm" {
  count = var.winvm_count

  name             = "${var.winvm_name_prefix}-${count.index + 1}"
  resource_pool_id = data.vsphere_compute_cluster.cluster.resource_pool_id
  datastore_id     = data.vsphere_datastore.ds.id
  folder           = var.vm_folder
  num_cpus         = var.winvm_cpu
  memory           = var.winvm_ram
  guest_id         = data.vsphere_virtual_machine.wintemplate.guest_id
  firmware         = data.vsphere_virtual_machine.wintemplate.firmware
  scsi_type        = data.vsphere_virtual_machine.wintemplate.scsi_type

  network_interface {
    network_id   = data.vsphere_network.network.id
    adapter_type = data.vsphere_virtual_machine.wintemplate.network_interface_types[0]
  }

  disk {
    label            = "${var.winvm_name_prefix}-${count.index + 1}-disk"
    size             = data.vsphere_virtual_machine.wintemplate.disks[0].size
    thin_provisioned = data.vsphere_virtual_machine.wintemplate.disks[0].thin_provisioned

    unit_number = data.vsphere_virtual_machine.wintemplate.disks[0].unit_number
  }

  clone {
    template_uuid = data.vsphere_virtual_machine.wintemplate.id
    customize {
    windows_options {
      computer_name = "WIN${random_string.vm_suffix.result}${count.index + 1}"
      admin_password = var.winvm_admin_password
      run_once_command_list = [
        "cmd.exe /c powershell -Command \"Set-NetConnectionProfile -Name 'corp.microsoft.com' -NetworkCategory Private\"",
        "cmd.exe /c powershell -Command \"Enable-PsRemoting\"",
        "cmd.exe /c powershell -Command \"winrm qc\"",
      ]
    }
      network_interface {}
      timeout = 120
    }
  }
}